plugins {
	id "com.diffplug.spotless" version "6.15.0"
	id "com.github.johnrengelman.shadow" version "7.1.2"
	id 'com.palantir.git-version' version '0.15.0'
	id 'idea'
	id 'java-library'
}

description = "kafka-connect-s3"

allprojects {
	apply plugin: 'idea'
	apply plugin: 'maven-publish'

	group = 'com.spredfast.kafka.connect.s3'
	version = '1.2.1'

	apply plugin: 'java-library'
	sourceCompatibility = 11
	targetCompatibility = 11

	dependencies {
		constraints {
			api("com.amazonaws:aws-java-sdk-s3:1.12.351")
			api("org.apache.kafka:connect-api:3.5.1")
			api("org.slf4j:slf4j-api:2.0.7")

			testImplementation("junit:junit:4.13.2")
			testImplementation("com.google.guava:guava:31.1-jre")
			testImplementation("javax.xml.bind:jaxb-api:2.4.0-b180830.0359")

			testImplementation("io.debezium:debezium-testing-testcontainers:2.0.0.Final")
			testImplementation("net.mguenther.kafka:kafka-junit:3.3.0")
			testImplementation("org.testcontainers:kafka:1.17.6")
			testImplementation("org.testcontainers:localstack:1.17.6")
		}
	}

	repositories {
		maven { url "https://repo.maven.apache.org/maven2" }
	}

	apply plugin: "com.diffplug.spotless"
	spotless {
		java {
			removeUnusedImports()
			cleanthat()
			googleJavaFormat()
			formatAnnotations()
		}
	}
}

apply plugin: "com.github.johnrengelman.shadow"
shadowJar {
	archiveClassifier = null
	dependencies {
		// provided in the connect classpath
		exclude(dependency('org.apache.kafka:connect-api'))
		exclude(dependency('org.apache.kafka:kafka-clients'))
		exclude(dependency('net.jpountz.lz4:.*:.*'))
		exclude(dependency('org.xerial.snappy:.*:.*'))
		exclude(dependency('org.slf4j:.*:.*'))
	}

	// for things we directly depend on, repackage so we don't conflict with other connectors
	relocate 'com.fasterxml', 'com.spredfast.shade.fasterxml'
	relocate 'org.apache.commons', 'com.spredfast.shade.apache.commons'
	relocate 'org.apache.http', 'com.spredfast.shade.apache.http'
	relocate 'org.joda', 'com.spredfast.shade.joda'
}

dependencies {
	implementation project(':sink')
	implementation project(':source')
}

tasks.register('javadocJar', Jar) {
	dependsOn javadoc
	archiveClassifier.set('javadoc')
	from subprojects*.tasks.javadoc.destinationDir
}

tasks.register('sourcesJar', Jar) {
	from subprojects*.sourceSets.main.allSource
	archiveClassifier.set('sources')
}

jar {
	from subprojects*.sourceSets.main.output
}

artifacts {
	archives javadocJar, sourcesJar
}

ext {
	pomHumanName = 'Kafka Connect S3'
	pomDesc = 'S3 Source and Sink Connectors for Kafka Connect'
}

publishing {
	publications {
		shadow(MavenPublication) { publication ->
			project.shadow.component(publication)
			groupId = 'io.sugarcrm'
		}
	}

	repositories {
		maven {
			credentials {
				username = System.getenv("MAVEN_USERNAME")
				password = System.getenv("MAVEN_PASSWORD")
			}

			url = "https://nexus.service.sugarcrm.com/repository/cxp-public/"
		}
		maven {
			name = 'GitHubPackages'
			credentials {
				username = System.getenv("USERNAME")
				password = System.getenv("TOKEN")
			}
			url = "https://maven.pkg.github.com/sugarcrm/kafka-connect-s3"
		}
	}
}
